#!nsh
#
# Standard apps for multirotors:
# att & pos estimator, att & pos control.
#

# The system is defaulting to INAV_ENABLED = 1
# but users can alternatively try the EKF-based
# filter by setting INAV_ENABLED = 0
if param compare INAV_ENABLED 1
then
	# The system is defaulting to EKF_ATT_ENABLED = 1
	# and uses the older EKF filter. However users can
	# enable the new quaternion based complimentary
	# filter by setting EKF_ATT_ENABLED = 0.
	# Note that on FMUv1, the EKF att estimator is not
	# available and the Q estimator runs instead.
	if param compare EKF_ATT_ENABLED 1
	then
		attitude_estimator_ekf start
		echo "attitude_estimator_ekf" >> $LOG_FILE
	else
		attitude_estimator_q start
		echo "attitude_estimator_q" >> $LOG_FILE
	fi
	position_estimator_inav start
	echo "position_estimator_inav" >> $LOG_FILE
else
	ekf_att_pos_estimator start
	echo "ekf_att_pos_estimator" >> $LOG_FILE
fi

if mc_att_control start
then
	echo "mc_att_control start" >> $LOG_FILE
else
	# try the multiplatform version
	mc_att_control_m start
	echo "mc_att_control_m start" >> $LOG_FILE
fi

if mc_pos_control start
then
	echo "mc_pos_control start" >> $LOG_FILE
else
	# try the multiplatform version
	mc_pos_control_m start
	echo "mc_pos_control_m start" >> $LOG_FILE
fi

#
# Start Land Detector
#
land_detector start multicopter
echo "land_detector start" >> $LOG_FILE
